set nocompatible
filetype plugin indent on
syntax enable

" Automatically set up vim-plug, if necessary
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')
Plug 'airblade/vim-gitgutter'           " display git status in signcolumn
Plug 'altercation/vim-colors-solarized' " load solarized colorscheme TODO: configuration https://vimawesome.com/plugin/vim-colors-solarized-ours
Plug 'benmills/vimux'                   " integrate vim with tmux
Plug 'docunext/closetag.vim'            " close HTML tags TODO: https://vimawesome.com/plugin/vim-closetag instead?
Plug 'chilcutt/vim-ripgrep'              " integration with ripgrep, support for :Rg
Plug 'junegunn/fzf'                     " base fzf integration repository
Plug 'junegunn/fzf.vim'                 " better vim support for fzf
Plug 'majutsushi/tagbar'                " adds integration with tags file
Plug 'maxmellon/vim-jsx-pretty'         " language support for jsx/tsx
Plug 'ngmy/vim-rubocop'                 " rubocop integration
Plug 'pangloss/vim-javascript'          " language support for js/es6
Plug 'prettier/vim-prettier'            " prettier integration
Plug 'scrooloose/nerdtree'              " tree file explorer
Plug 'sheerun/vim-polyglot'             " TODO - replace with individual plugins
Plug 'tommcdo/vim-lion'                 " align text around a character
Plug 'tpope/vim-commentary'             " comment out blocks of lines
Plug 'tpope/vim-endwise'                " close ruby blocks automatically TODO - is there something better out there?
Plug 'tpope/vim-fugitive'               " git integration
Plug 'tpope/vim-rails'                  " ruby on rails integration
Plug 'tpope/vim-rake'                   " rake integration for non-rails ruby projects
Plug 'tpope/vim-rhubarb'                " github-specific git integration
Plug 'tpope/vim-surround'               " surrounding movement
Plug 'vim-scripts/ZoomWin'              " ctrl+w o to zoom
Plug 'vim-scripts/matchit.zip'          " adds support for more % matches
Plug 'w0rp/ale'                         " asynchronous linting
Plug 'godlygeek/tabular'                " dependency for vim-markdown
Plug 'plasticboy/vim-markdown'          " utilities for md files
Plug 'iamcco/markdown-preview.nvim', { 'do': 'cd app & yarn install'  } " preview support for markdown
Plug 'junegunn/goyo.vim'                " remove editor decoration
Plug 'junegunn/limelight.vim'           " focus on current block
call plug#end()

set autoindent                                             " automatically indent when hitting <cr>
set backspace=indent,eol,start                             " set backspace to work correctly with autoindent, line breaks, and beyond insert mode start
set cmdheight=2                                            " increase the space for commands so the window doesn't feel jumpy
set complete-=i                                            " exclude included files from completion
set display+=lastline                                      " show as much of lines as possible instead of @
set expandtab                                              " when pressing tab, insert spaces
set hidden                                                 " allow buffers to be hidden, don't force writting before navigating away
set hlsearch                                               " highlight all search matching
set ignorecase                                             " perform case-insensitive searches
set laststatus=2                                           " always show the statusline
set lazyredraw                                             " redraw less often for smoother scrolling
set list                                                   " show whitespace characters in listchars
set listchars=tab:>\ ,trail:\.,extends:>,precedes:<,nbsp:+ " mark tabs, trailing spaces, non-breakable spaces
set nowrap                                                 " default to not wrapping long lines
set nrformats=alpha,hex,bin                                " set alpha, 0x, and 0b to be incremented, disable octal because decimal is often zero-padded
set number                                                 " display the line number in before each line
set shiftwidth=2                                           " set indentation with >> to 2
set signcolumn=yes                                         " always show the signcolumn so it doesn't move when activated, vim-gitgutter & ale
set smartcase                                              " when a search query has a capital letter, do an exact match search
set smarttab                                               " enable tabbing based on shiftwidth and tabstop
set softtabstop=2                                          " set each tab to insert 2 spaces with expandtab
set statusline+=%F                                         " display the filename in the statusline
set t_Co=16                                                " set low number of colors to make highlights stick out, match bright colors without forcing bold
set tabstop=2                                              " display tabs as 2 spaces wide
set ttyfast                                                " send characters to terminal quickly for smoother experience
set updatetime=100                                         " write swp faster, to trigger faster git status reaction, vim-gitgutter
set wildmenu                                               " enable tab completion from insert mode

" When using tmux, use escape sequence to get t_RB value from the outer shell
" so we can set the background value correctly.
if exists("$TMUX")
  let &t_RB = "\ePtmux;\e\e]11;?\007\e\\"
endif

colorscheme solarized

" set an 80 guide to try to keep lines short
highlight ColorColumn ctermbg=black
let &colorcolumn=join(range(81,999),",")

" highlight all tabs in red
autocmd ColorScheme * highlight AllTabs ctermbg=red guibg=red
highlight AllTabs ctermbg=red guibg=red
match AllTabs /\t/

" highlight trailing whitespace
highlight ExtraWhitespace ctermbg=red guibg=red
match ExtraWhitespace /\s\+$/
autocmd BufWinEnter * match ExtraWhitespace /\s\+$/
autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/
autocmd InsertLeave * match ExtraWhitespace /\s\+$/
autocmd BufWinLeave * call clearmatches()

let g:fzf_layout = { 'down': '~20%' }

" use fzf and rg for advanced find
command! -bang -nargs=* Find call fzf#vim#grep('rg --column --line-number --no-heading --fixed-strings --ignore-case --no-ignore --hidden --follow --glob "!.git/*" --color "always" '.shellescape(<q-args>).'| tr -d "\017"', 1, <bang>0)

" always show bookmarks
let NERDTreeShowBookmarks=1
let g:tagbar_left = 1 " open tagbar on the left
let g:tagbar_autoclose = 1 " close tagbar after selection
let g:tagbar_autofocus = 1 " focus when opened
let g:VimuxHeight = "25" "set vimux terminal height

autocmd BufNewFile,BufRead *.es6 set filetype=javascript

let g:vim_json_syntax_conceal = 0 " don't do weird shit in json files

let g:jsx_ext_required = 0 " handle jsx in js files

" ripgrep configuration
let g:rg_highlight=1

" let g:polyglot_disabled = ['graphql']

" vim-gitgutter
let g:gitgutter_enabled = 0 " disable by default

" vim-jsx-pretty
let g:vim_jsx_pretty_colorful_config = 1

" vim-lion
let b:lion_squeeze_spaces = 1 " eliminate excess preceeding spaces

let mapleader = "\<Space>" " set leader to spacebar
nmap <leader>w :w<cr>
nmap <leader>q :q<cr>
nmap <leader>x :x<cr>
nmap <leader>o o<esc>
nmap <leader>O O<esc>
map <leader>y "+y
map <leader>p "+p
inoremap jk <esc> " set jk to escape to normal mode

nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L> " clear hlsearch with ctrl+l

nmap <leader>v :vsp<cr><C-w>l
nmap <leader>s :sp<cr><C-w>j
nmap <leader><space> <C-w><C-w>
nmap <leader>h <C-w>h
nmap <leader>j <C-w>j
nmap <leader>k <C-w>k
nmap <leader>l <C-w>l

nmap <leader>t :tabnew<cr>

" nmap <leader>r :NERDTreeFind<cr>
" nmap <leader>d :NERDTreeToggle<cr>

nmap <c-t> :TagbarToggle<cr>

nmap <leader>f :Rg<space>
nmap <leader>F :Find<space>
nnoremap <silent> <C-P> :Files<cr>

map <leader>gs :GitGutterToggle<cr>
map <leader>gb :Gblame<cr>
map <leader>gw :Gbrowse<cr> " open file in browser at Github

let s:ruby_runner = "bundle exec rspec"
let s:javascript_runner = "yarn run jest"
let s:javascript_debug_runner = "node --inspect-brk node_modules/.bin/jest --runInBand"

function RunTestFile()
  if &filetype ==# "javascript"
    call VimuxRunCommand(s:javascript_runner . " " . bufname("%"))
  elseif &filetype ==# "ruby"
    call VimuxRunCommand(s:ruby_runner . " " . bufname("%"))
  endif
endfunction

function RunTestLine()
  if &filetype ==# "javascript"
    call VimuxRunCommand(s:javascript_runner . " " . bufname("%"))
  elseif &filetype ==# "ruby"
    call VimuxRunCommand(s:ruby_runner . " " . bufname("%") . ":" . line("."))
  endif
endfunction

function RunTestDebugger()
  if &filetype ==# "javascript"
    call VimuxRunCommand(s:javascript_debug_runner . " " . bufname("%"))
  endif
endfunction

function RunTestWatch()
  if &filetype ==# "javascript"
    call VimuxRunCommand(s:javascript_runner . " " . bufname("%") . " --watch")
  endif
endfunction

nmap <leader>rf :call RunTestFile()<cr>
nmap <leader>rs :call RunTestLine()<cr>
nmap <leader>rd :call RunTestDebugger()<cr>
nmap <leader>rw :call RunTestWatch()<cr>
nmap <leader>bb :VimuxCloseRunner<cr>
nmap <leader>bc :VimuxPromptCommand<cr>
nmap <leader>br :VimuxRunLastCommand<cr>

nmap <leader>a :A<cr>

xmap <leader>/ <Plug>Commentary<cr>
nmap <leader>/ <Plug>CommentaryLine<cr>

nmap <leader>n :n<cr>
nmap <leader>N :N<cr>
nmap <leader>cn :cnext<cr>
nmap <leader>cp :cprevious<cr>
nmap <leader>ln :ALENext<cr>
nmap <leader>lp :ALEPrevious<cr>

let g:vimrubocop_keymap = 0 " disable default mappings
map <leader>R :RuboCop -a<cr>

map <leader>P :PrettierAsync<cr>

let g:ale_echo_msg_format = "%linter% %code: %%s"
let g:ale_linters = {
                  \   "javascript": ["eslint", "prettier", "flow-language-server"],
                  \   "typescript": ["eslint", "tsserver"],
                  \   "typescriptreact": ["eslint", "prettier"]
                  \ }
let g:ale_fixers = {
                 \   "ruby": ["rubocop"],
                 \   "javascript": ["eslint", "prettier"],
                 \   "typescript": ["eslint", "prettier"],
                 \   "typescriptreact": ["prettier"]
                 \ }
let g:ale_javascript_prettier_use_local_config = 1
let g:ale_fix_on_save = 1 " automatically fix when saving
let g:ale_completion_enabled = 0 " disable for now, its annoying

let s:javascript_debug_line = "debugger"
let s:ruby_debug_line = "require 'pry'; binding.pry"

function! FlipDebugger()
  if &filetype ==# "javascript"
    if getline('.') =~? s:javascript_debug_line
      normal dd
    else
      execute "normal" "o" . s:javascript_debug_line
    endif
  elseif &filetype ==# "ruby"
    if getline('.') =~? s:ruby_debug_line
      normal dd
    else
      execute "normal" "o" . s:ruby_debug_line
    endif
  endif
  write
endfunction

nnoremap <leader>d :call FlipDebugger()<cr>

" disable markdown folding
let g:vim_markdown_folding_disabled = 1

" update preview only when exiting insert mode
let g:mkdp_refresh_slow = 1

" set width while in goyo (default: 80)
let g:goyo_width = 100

" custom commands for goyo
function! s:goyo_enter()
  set noshowmode
  set noshowcmd

  " keep cursor in middle of the line
  set scrolloff=999

  " wrap long lines that are typical in markdown
  set wrap

  " enable limelight
  Limelight
endfunction

function! s:goyo_leave()
  set showmode
  set showcmd
  set scrolloff=5
  set nowrap
  Limelight!
endfunction

autocmd! User GoyoEnter nested call <SID>goyo_enter()
autocmd! User GoyoLeave nested call <SID>goyo_leave()

" limelight configuration
set t_Co=256
let g:limelight_conceal_ctermfg = 'gray'
let g:limelight_conceal_ctermfg = 240

function! MarkdownMode()
  MarkdownPreview
  Goyo
endfunction

nmap <leader>md :call MarkdownMode()<cr> " launch markdown preview and start goyo mode
